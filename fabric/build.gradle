plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

archivesBaseName = "${archivesBaseName}-fabric"

architectury {
    platformSetupLoomIde()
    fabric()
}

fabricApi {
    configureDataGeneration()
}

loom {
    runs {
        client {
            programArgs(["--username", "samsthenerd", "--uuid", "6f07899c-2b26-4221-8033-1f53f7a0e111"])
        }
    }
}

loom {
    runs{
        datagen {
//            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(':common').file('src/main/generated')}"
            vmArg "-Dfabric-api.datagen.modid=${project.mod_id}"
            runDir "build/datagen"
        }
    }
}

tasks {
    tasks.withType(Jar).configureEach {
        exclude("architectury.common.json")
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

tasks.register("jenkinsArtifacts", Copy){
    from remapJar, remapSourcesJar
    into "${rootDir.toPath()}/build/jenkinsArtifacts"
}

build {
    dependsOn tasks.named("jenkinsArtifacts")
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}


sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }

    repositories {
        maven {
            url System.getenv("local_maven_url")
        }
    }
}


publishMods {
    file = remapJar.archiveFile
    additionalFiles.from(sourcesJar.archiveFile)
    changelog = trimChangelog(file("$rootDir/changelog.md").text)
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")

    version = project.version + "-fabric"
    displayName = "Month of Swords" + " " + project.version + " [Fabric]"

    curseforge {
        accessToken = System.getenv("CURSEFORGE_API_KEY")
        projectId = project.mos_curseforge_id
        minecraftVersions.add(project.minecraft_version)
        requires{
            slug = "fabric-api"
        }
        requires{
            slug = "architectury-api"
        }
    }

    modrinth {
        accessToken = System.getenv("MODRINTH_API_KEY")
        projectId = project.mos_modrinth_slug
        minecraftVersions.add(project.minecraft_version)

        requires{
            slug = "fabric-api"
        }
        requires{
            slug = "architectury-api"
        }
    }

    github {
        accessToken = System.getenv("GITHUB_TOKEN") ?: ""
        repository = System.getenv("GITHUB_REPOSITORY") ?: ""
        commitish = System.getenv("GIT_COMMIT") ?: ""
        tagName = "release/${project.version}"
    }
}

tasks {
    publishGithub {
        dependsOn(
                project(":common").tasks.remapJar,
                project(":neoforge").tasks.remapJar,
        )

        // we need to do this here so that it waits until Forge is already configured
        // otherwise tasks.remapJar doesn't exist yet
        publishMods {
            github {
                additionalFiles.from(
                        project(":common").tasks.remapJar.archiveFile,
                        project(":neoforge").tasks.remapJar.archiveFile,
                )
            }
        }
    }
}